import wollok.game.*
import objetosDePacman.*
import fantasmas.*
import pacman.*
import comidas.*

program gameW {
	
  // CONFIG	
	game.title("WollokPac")
	game.height(10)
	game.width(14)
	game.ground("fondoNegro.png")
  // FANTASMASS 

	var verde = new Fantasma(image = "fantasmaVerde.png", nombre = "Fantasma Verde", position = game.at(2,3) )
    var amarillo = new Fantasma(image = "fantasmaAmarilLo.png", nombre = "Fantasma Amarrilo", position = game.at(5,6))
	var violeta = new Fantasma(image = "fantasmaVioleta.png", nombre = "Fantasma Violeta", position = game.at(4,2))
	var rojo = new Fantasma(image = "fantasmaRojo.png", nombre = "Fantasma Rojo", position = game.at(1,5) ) 
	

	//Comidas
	 var cereza1= new Cereza(image="cereza.png",position= game.at(11,1))
	 var cereza2= new Cereza(image="cereza.png",position= game.at(7,6))
	 var pera1= new Pera(image="pera.png",position= game.at(1,9))
	 var pera2= new Pera (image= "pera.png",position= game.at(5,2)) 
	
	
    //TECLADO 
    
    keyboard.up().onPressDo { pacman.position(pacman.position().up(1)) pacman.image("pacmanBocaArriba.png") }
	keyboard.down().onPressDo { pacman.position(pacman.position().down(1)) pacman.image("pacmanBocaAbajo.png") }
	keyboard.left().onPressDo { pacman.position(pacman.position().left(1)) pacman.image("pacmanIzquierda.png") }
	keyboard.right().onPressDo { pacman.position(pacman.position().right(1)) pacman.image("pacman.png")}

	//SE AGREGA EL PACMAN AL TABLERO
	game.addVisual(pacman)
	
	// LISTA DE COMIDAS
	 const comidas = [cereza1,cereza2,pera1,pera2]
	/*
	 *  Se agregan las comidas al tablero
	 *  Cuando el pacman toca una comida la come 
	*/
	 comidas.forEach{comida =>
	 	game.addVisual(comida)
	 	game.whenCollideDo(comida, {pacman => pacman.comer(comida)})
	 }
	 
	//LISTA DE FANTASMAS
    const fantasmas = [verde, amarillo, violeta, rojo]
    /*
	 *  Se agregan los fantasmas al tablero
	 *  Cuando el pacman toca un fantasma pierde una vida 
	*/
     fantasmas.forEach{fantasma =>
     	game.addVisual(fantasma)
		game.whenCollideDo(fantasma, { pacman => pacman.chocarCon(fantasma) })
		game.onTick(1.randomUpTo(5) * 1000, "Fantasma", {fantasma.acercarseA(pacman)})
			
	}
	
	game.start()
}
